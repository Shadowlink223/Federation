ACTOR Legionary : Reaver 14367
{
	//$Category Federation
  Health 380
  Painchance 30
  PainChance "Fire2", 0
  Damagefactor "Fire2", 0.1
  PainChance "Shock", 255
  Damagefactor "Shock", 1.5
  Damagefactor "head", 0.0
  Speed 18
  Radius 20
  Height 60
  Species hellknight
  Monster
  +NOBLOOD
  +NOTARGET
  +DONTHURTSPECIES
  +FASTER
  MinMissileChance 150
  MaxDropoffHeight 32
  Mass 500
  
  DropItem "Cell", 32
  DropItem "FlameAmmo" 128
  DropItem "LargeFlameAmmo" 32
  
  DropItem "Flamethrower" 1
  
  SeeSound "Legionary/sight"
  PainSound "Legionary/pain"
  DeathSound "Legionary/death"
  ActiveSound "Legionary/active"
  HitObituary "%o was slashed by a Legionary."
  Obituary "%o was blasted by a Legionary."

  States
  {
  Spawn:
    ROB1 A 0
    ROB1 A 0 Thing_SetSpecial(0,226,613,0,random(30,40))
    ROB1 A 10 A_Look
    Goto Spawn+2
  See:
    ROB1 BB 3 A_Chase
    ROB1 A 0 A_PlaySound("Hoplite/pain")
    ROB1 CC 3 A_Chase
    ROB1 DD 3 A_Chase
    ROB1 A 0 A_PlaySound("Hoplite/pain")
    ROB1 EE 3 A_Chase
    Loop
  Melee:
    ROB1 H 6 A_FaceTarget
    ROB1 I 8 A_CustomMeleeAttack(random(4,12)*3, "Legionary/blade")
    ROB1 H 6
    Goto See
  Missile:
	ROB1 F 0 A_Jumpifcloser(512,"FireAttackTarget")
    ROB1 F 8 A_FaceTarget
    ROB1 F 0 A_PlaySound("Legionary/attack")
    ROB1 G 0 BRIGHT A_CustomBulletAttack(11,11,3,random(1,3)*3,"BulletPuff")
	ROB1 G 0 BRIGHT A_CustomMissile("LegionaryTracer",38,0,random(-11,11),2,random(-10,10))
	ROB1 G 0 BRIGHT A_CustomMissile("LegionaryTracer",38,0,random(-11,11),2,random(-10,10))
	ROB1 G 11 BRIGHT A_CustomMissile("LegionaryTracer",38,0,random(-11,11),2,random(-10,10))
    Goto See
  FireAttackTarget:
	ROB1 F 10 A_FaceTarget                                       				//
  FireAttack:
	TNT1 A 0 A_JumpIfInventory("LegionaryCounter",16,5)                  		// FOR LOOP {
    ROB1 G 2 Bright A_CustomMissile("FKFire",28,0)               				// 1ST FIRE
    ROB1 G 2 Bright A_CustomMissile("FKFire",28,0)               				// 2ND FIRE
    ROB1 F 0 A_FaceTarget                                        				//
	TNT1 A 0 A_GiveInventory("LegionaryCounter",1)								// LEGIONARYCOUNTER++ }
	loop
	ROB1 F 0 A_TakeInventory("LegionaryCounter",16)								// /*RESET LEGIONARYCOUNTER*/
    ROB1 F 1 A_SentinelRefire                                    				// KEEP SHOOTING!
    Goto See																	//
  Pain:
    ROB1 A 2 A_TossGib
    ROB1 A 2 A_Pain
    Goto See
  Pain.KillMe:
    TNT1 A 0
    Goto Missile
  Death:
    ROB1 J 6
    ROB1 K 6 A_Scream
    ROB1 L 5
    ROB1 M 5 A_NoBlocking
    ROB1 NOP 5
    ROB1 Q 6 A_Explode(32,32,1,1)
    ROB1 R -1
    Stop
  XDeath:
    ROB1 L 5 A_TossGib
    ROB1 M 5 A_Scream
    ROB1 N 5 A_TossGib
    ROB1 O 5 A_NoBlocking
    ROB1 P 5 A_TossGib
    Stop
  }
}


actor LegionaryCounter : inventory { Inventory.MaxAmount "16" }